get_property(cmakeit_POPULATED GLOBAL PROPERTY cmakeit_POPULATED)
if(NOT cmakeit_POPULATED)
  set(EXTERNALS_PATH "${CMAKE_SOURCE_DIR}/_externals/" CACHE PATH "")
  set(cmakeit_URL "https://gitlab.com/nordfox/cmakeit/-/archive/develop/cmakeit-develop.tar.gz" CACHE STRING "")
  if(DEFINED cmakeit_URL_CURRENT AND NOT ("${cmakeit_URL}" STREQUAL "${cmakeit_URL_CURRENT}"))
    unset(cmakeit_PATH CACHE)
  endif()
  set(cmakeit_URL_CURRENT "${cmakeit_URL}" CACHE INTERNAL "")
  
  get_filename_component(cmakeit_ARCHIVE "${cmakeit_URL}" NAME)
  set(cmakeit_ARCHIVE_PATH "${EXTERNALS_PATH}/${cmakeit_ARCHIVE}" CACHE PATH "")
  get_filename_component(cmakeit_ARCHIVE_TAG "${cmakeit_ARCHIVE_PATH}" NAME_WE) 
  
  set(cmakeit_PATH "${EXTERNALS_PATH}/${cmakeit_ARCHIVE_TAG}" CACHE PATH "")
  if(NOT EXISTS "${cmakeit_PATH}")
    if(NOT EXISTS "${cmakeit_ARCHIVE_PATH}")
      message(STATUS "Downloading cmakeit")
      file(DOWNLOAD "${cmakeit_URL}" "${cmakeit_ARCHIVE_PATH}" SHOW_PROGRESS)
    endif()
    message(STATUS "Extracting cmakeit")
    execute_process(COMMAND "${CMAKE_COMMAND}" -E tar -xf "${cmakeit_ARCHIVE_PATH}" WORKING_DIRECTORY "${EXTERNALS_PATH}")
  endif()
  
  set(cmakeit_LOADER_REVISION 1)
  if(EXISTS "${cmakeit_PATH}/cmi_include.cmake")
    include("${cmakeit_PATH}/cmi_include.cmake")
  endif()
  
  set_property(GLOBAL PROPERTY cmakeit_POPULATED "TRUE")
  set(CMAKE_MODULE_PATH "${CMAKE_MODULE_PATH}" "${cmakeit_PATH}/modules")
endif()
